// Main hot-keys rendering function.
function u16 AchievementsPlus.HotkeysManager.drawHotkey(string key, string text, s16 px, s16 py, u16 renderQueue)
{
	// Additional offset to render the text
	u8 tempValue0 = AchievementsPlus.HotkeysManager.getTextOffset(key)
	AchievementsPlus.TextManager.drawText(px + 0x0a + tempValue0, py - 0x01, text, FONT_SMALLFONT, FORMAT_UPPERCASE, ALIGN_LEFT, 0x01, renderQueue)
	
	// Draw hot-key
	string tempValue8 = AchievementsPlus.HotkeysManager.getHotkeySprite(key)
	Renderer.drawCustomSprite(tempValue8, px, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
	
	// Additional offset to render the tape
	u16 tempValue1 = AchievementsPlus.HotkeysManager.getTapeOffset(key)
	
	// Get text width for tape rendering
	s32 tempValue2 = AchievementsPlus.TextManager.getTextWidth(text, FONT_SMALLFONT, FORMAT_UPPERCASE, 0x01) + 0x05
	
	// Draw background
	Renderer.drawCustomSprite(stringformat("menu_hotkey_backgroundHold_x%d", tempValue2), px + tempValue1, py, 0x00, SPRITE_FLAG_PRIO, renderQueue, 0x00, min(achievementsplus.buttons.tempValue4 * 0x08, 255))
	Renderer.drawCustomSprite(stringformat("menu_hotkey_background_x%d", tempValue2), px + tempValue1, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
	
	px += tempValue2
	Renderer.drawCustomSprite("menu_hotkey_backgroundHold_end", px + tempValue1, py, 0x00, SPRITE_FLAG_PRIO, renderQueue, 0x00, min(achievementsplus.buttons.tempValue4 * 0x08, 255))
	Renderer.drawCustomSprite("menu_hotkey_background_end", px + tempValue1, py, 0x00, SPRITE_FLAG_PRIO, renderQueue)
	
	// Return hot-key width
	return tempValue2 + 0x08 + tempValue1 + 0x08
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - //
// Support tools
// - - - - - - - - - - - - - - - - - - - - - - - - - - - //


// The function returns the sprite for the specified key.
function string AchievementsPlus.HotkeysManager.getHotkeySprite(string key)
{
	if (SETTING_ACHIEVEPLUS_PS4_HOTKEYS)
		return "menu_hotkey_ps4_" + key
	
	return "menu_hotkey_" + key
}


// The function returns the text offset for the specified key.
function u8 AchievementsPlus.HotkeysManager.getTextOffset(string key)
{
	if (SETTING_ACHIEVEPLUS_PS4_HOTKEYS)
	{
		if (key == "dpadLeft" || key == "dpadRight") 
			return 0x04
	}
	else
	{
		if (key == "Start")
			return 15
	}
	
	// Fallback value
	return 0x00
}


// The function returns the tape offset for the specified key.
function u8 AchievementsPlus.HotkeysManager.getTapeOffset(string key)
{
	if (SETTING_ACHIEVEPLUS_PS4_HOTKEYS)
	{
		if (key == "dpadLeft" || key == "dpadRight") 
			return 0x0a
		
		return 0x06
	}
	else
	{
		if (AchievementsPlus.HotkeysManager.getTextOffset(key) > 0x00)
			return 21
		
		return 0x07
	}
	
	// Fallback value
	return 0x00
}